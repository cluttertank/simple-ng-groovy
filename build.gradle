buildscript {
  ext {
    springBootVersion = "1.0.0.RELEASE"
    stanfordCoreNLPVersion = "3.3.1"
    log4jVersion = "1.2.17"
  }
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'

springBoot {
  mainClass = 'groovboot.App'
}

jar {
  baseName = 'groovboot'
  version =  '1.0'
}

repositories {
  mavenCentral()
}

configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'org.apache.tomcat.embed') {
      details.useVersion '8.0.3'
    }
  }
}

tasks.withType(GroovyCompile) {
  groovyOptions.optimizationOptions.indy = true
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.3.0-beta-1:indy'
  compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
  compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
  compile "edu.stanford.nlp:stanford-corenlp:${stanfordCoreNLPVersion}"
  compile "log4j:log4j:${log4jVersion}"
  testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
  //compile "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}
